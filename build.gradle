plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
    id "com.jfrog.bintray" version "1.8.4"
}

group 'com.neva.commons'
description = 'Gitignore File Filter'
defaultTasks = ['publishToMavenLocal']

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

bintray {
    user = project.findProperty('bintrayUser') ?: System.getenv('BINTRAY_USER')
    key = project.findProperty('bintrayKey') ?: System.getenv('BINTRAY_KEY')
    publications = ['mavenJava']
    pkg {
        repo = 'maven-public'
        name = 'gitignore-file-filter'
        userOrg = 'neva-dev'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/neva-dev/gitignore.git'
        labels = ['gitignore', 'git', 'vcs']
        version {
            name = project.version
            desc = "${project.description} ${project.version}"
            vcsTag = project.version
        }
    }
    publish = true
}
